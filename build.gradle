import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()

    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.1"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

group 'net.ossindex'
version '0.4.7-beta11'

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

// The code depends on a specially shadowed copy of mapdb
tasks.compileJava.dependsOn(':ossi-mapdb:shadowJar')

dependencies {
    shadow localGroovy()
    shadow gradleApi()

    implementation 'org.sonatype.goodies:package-url-java:1.0.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.7'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'org.apache.httpcomponents:httpcore:4.4.11'
    implementation 'commons-codec:commons-codec:1.10'
    implementation 'net.ossindex:heuristic-version:2.0.0'
    implementation 'org.mockito:mockito-core:2.18.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.slf4j:slf4j-simple:1.7.25'

    // Use our shadow copy of mapdb
    compile files ('ossi-mapdb/build/libs/ossi-mapdb-3.0.7.jar')

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.13'
    testImplementation gradleTestKit()
    testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

pluginBundle {
    website = 'https://ossindex.sonatype.org/'
    vcsUrl = 'https://github.com/ossindex/ossindex-gradle-plugin'
    description = 'Checks dependencies for known vulnerabilities'
    tags = ['cve', 'audit', 'security', 'dependencies']

    plugins {
        greetingsPlugin {
            id = 'net.ossindex.audit'
            displayName = 'OSSIndex Gradle Audit'
        }
    }
}

task showMeCache {
    doLast {
        configurations.compile.each { println it }
    }
}

// Ensure we package our shadow jar
jar {
    from {
        zipTree('ossi-mapdb/build/libs/ossi-mapdb-3.0.7.jar')
    }
}

wrapper {
    gradleVersion = "5.2"
    distributionType = Wrapper.DistributionType.ALL
}
